1*PASCAL BOURGUIGNON 20/06/2014
2*EXAMPLE D'AFFICHAGE DE GRAPHE SUR UN TERMINAL TEKTRONIC 4014
9* LANCER LSE AVEC CES VARIABLES D'ENVIRONNEMENT DANS UN XTERM:
10* export LSE_OUTPUT_ARROWS=UNICODE-HAFLWIDTH
11* export LSE_ACCENTED_OUTPUT=T
12* export LSE_PAGER=T
13* export LSE_REJECT_LOWCASE_INPUT=NIL
14* export LSE_MODERN_MODE=NIL
15* export LSE_RETURN_IS_XOFF=T
16* export LSE_UPCASE_OUTPUT=NIL
17* export LSE_NO_BELL=NIL
50 PI_3.141592
51 &TEKIN();*INITIALISE LE TERMINAL GRAPHIQUE
52 X0_512;Y0_390;*L'ORIGINE
53 MAX_510;MAY_380;* [X0-MAX,X0+MAX]x[Y0-MAY,Y0+MAY]
54 FAIRE 63 POUR P_1 PAS 0.1 JUSQUA 10 
55 &TEKEE();*EFFACE ECRAN
56 &TEKDE(X0-MAX,Y0);&TEKLI(X0+MAX,Y0);* AXE HORIZONTAL
57 &TEKDE(X0,Y0-MAY);&TEKLI(X0,Y0+MAY);* AXE VERTICAL
58 PREM_1
59 FAIRE 64 POUR X_-PI PAS 0.02 JUSQUA PI
60 Y_&F(X,P)
61 TX_ENT(X0+X/PI*MAX);TY_ENT(Y0+Y*MAY)
62 SI PREM=1 ALORS DEBUT PREM_0;&TEKDE(TX,TY) FIN SINON &TEKLI(TX,TY)
63*FIN BOUCLE COURBE
64 &ATTEN(1);*FIN BOUCLE PARAMETRE
65 TERMINER
100 PROCEDURE &F(X,P) LOCAL X,P;RESULTAT (SIN(X)+SIN(P*X))/3
101 PROCEDURE &ATTEN(DT) LOCAL DT,T0,T;T0_TEM()
102 T_TEM();SI T<T0+DT ALORS ALLER EN 102
103 RETOUR
180*
181* &TEKIN()     INITIALISE LE TERMINAL TEKTRONIC 4014.
182* &TEKEE()     EFFACE L'ECRAN DU TERMINAL TEKTRONIC 4014.
183* &TEKDE(X,Y)  DEPLACE LE CRAYON SANS TRACER.
184* &TEKLI(X,Y)  DEPLACE LE CRAYON EN TRACANT UNE LIGNE DROITE.
185* &TEKPO(X,Y)  DEPLACE LE CRAYON SANS TRACER, ET TRACE UN POINT.
186* QUELQUES VARIABLES GLOBALES DONT LE NOM COMMENCE PAR TK SONT UTILISEES.
187*
200*UTILITAIRES POUR AFFICHER DES GRAPHIQUES SUR UN TERMINAL TEKTRONIC 4014
201 PROCEDURE &TEKIN();*INITIALISATION
202 CHAINE TKESC,TKFF,TKGS;TKESC_EQC(27);TKFF_EQC(12);TKGS_EQC(29);TKLHX_0;TKLLX_0;TKLHY_0;TKLLY_0;*GLOBALES
203 AFFICHER[U,'[?38',U]TKESC,EQC(104);RETOUR
204 PROCEDURE &TEKEE();AFFICHER[U,U]TKESC,TKFF;RETOUR;*EFFACE ECRAN GRAPHIQUE
205 PROCEDURE &TEKLI(X,Y)LOCAL X,Y,HX,LX,HY,LY,SHIX;* ENVOI COORDONNEES/LIGNE
206 HX_32+ETL(X/32,31);LX_64+ETL(X,31);HY_32+ETL(Y/32,31);LY_96+ETL(Y,31)
207 SI HY#TKLHY ALORS AFFICHER[U]EQC(HY)
208 SI LY#TKLLY OU HX#TKLHX ALORS AFFICHER[U]EQC(LY)
209 SI HX#TKLHX ALORS AFFICHER[U]EQC(HX)
210 AFFICHER[U]EQC(LX);TKLHX_HX;TKLLX_LX;TKLHY_HY;TKLLY_LY;RETOUR
211 PROCEDURE &TEKDE(X,Y)LOCAL X,Y;AFFICHER[U]TKGS;&TEKLI(X,Y);RETOUR;*DEPART
212 PROCEDURE &TEKPO(X,Y)LOCAL X,Y;&TEKDE(X,Y);AFFICHER[U]EQC(TKLLX);RETOUR;*POINT
