251*EXTRAIT DE "LANGAGE SYMBOLIQUE D'ENSEIGNEMENT L.S.E. MANUEL D'UTILISATION"
252*BULLETIN DE LIAISON "L'INFORMATIQUE ET L'ENSEIGNEMENT SECONDAIRE" JAN 1975
253*C.LAFOND - INSTITUT NATIONAL DE RECHERCHE ET DE DOCUMENTATION PEDAGOGIQUES
254*PROGRAMME PG47, PAGE 103, JEU DE BATAILLE NAVALE
255*[PAS TESTE SUR EMULSE]
1*JEU DE BATAILLE NAVALE.PROGRAMME PRINCIPAL
2 AFFICHER 'PREPAREZ UNE GRILLE DE 5*5 AVEC 4 BATEAUX.'
3 AFFICHER 'JE FAIS DE MEME.TAPEZ CO QUAND VOUS SERZ PRETS.'
5 TABLEAU A[5,5],B[5,5;CHAINE REP
6 FAIRE 6 POUR I_1 JUSQUA 5;FAIRE 6 POUR J_1 JUSQUA 5;A[I,J]_0;B[I,J]_0
10* PREPARATION DE LA GRILLE-MACHINE
11 FAIRE 11 POUR Z_1 JUSQUA 4;&PROC1(A);&PROC2(A)
12 PAUSE
15 VOUS_0;MOI_0;* TIR DU JOUEUR
16 AFFICHER 'A VOUS : ';LIRE I,J
17 SI A[I,J]#2 ALORS DEBUT AFFICHER[5X,'A L''EAU'];ALLER EN 21 FIN
18 VOUS_VOUS+1;AFFICHER[5X,'COULE']
20* TIR DE LA MACHINE
21 &PROC1(B)
22 AFFICHER[/,'A MOI  : ',2U,3X,'? ']I,J;LIRE REP
23 SI REP='COULE' ALORS DEBUT MOI_MOI+1;&PROC2(B) FIN SINON B[I,J]_1
25 AFFICHER '';SI VOUS<4 ET MOI<4 ALORS ALLER EN 16
26 SI VOUS+MOI=8 ALORS DEBUT AFFICHER 'MATCH NUL';ALLER EN 28 FIN
27 SI VOUS=4 ALORS AFFICHER 'VOUS AVEZ GAGNE' SINON AFFICHER 'J''AI GAGNE'
28 AFFICHER 'MA GRILLE ',A,'VOTRE GRILLE ',B;TERMINER
50 PROCEDURE &PROC1(C)
51 I_ENT(5*ALE(0)+1);J_ENT(5*ALE(0)+1);SI C[I,J]=0 ALORS RETOUR
52 FAIRE 54 POUR M_1 JUSQUA 5;FAIRE 53 POUR N_1 JUSQUA 5
53 J_J+3;SI J>5 ALORS J_J-5;SI C[I,J]=0 ALORS RETOUR
54 I_I+3;SI I>5 ALORS I_I-5
60 PROCEDURE &PROC2(D)
61 FAIRE 62 POUR K_I-1 JUSQUA I+1;FAIRE 62 POUR L_J-1 JUSQUA J+1
62 SI K>0 ET K<6 ET L>0 ET L<6 ALORS D[K,L]_1
63 D[I,J]_2;RETOUR


