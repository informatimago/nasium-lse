251*EXTRAIT DE "LANGAGE SYMBOLIQUE D'ENSEIGNEMENT L.S.E. MANUEL D'UTILISATION"
252*BULLETIN DE LIAISON "L'INFORMATIQUE ET L'ENSEIGNEMENT SECONDAIRE" JAN 1975
253*C.LAFOND - INSTITUT NATIONAL DE RECHERCHE ET DE DOCUMENTATION PEDAGOGIQUES
254*PROGRAMME PG54, PAGE 115, TRADUCTION D'UNE DATE DE FRANCAIS EN ANGLAIS
255*[PAS TESTE SUR EMULSE]
1* TRADUCTION D'UNE DATE DE FRANCAIS EN ANGLAIS
2* AVEC VERIFICATION DE LA VALIDITE DE CETTE DATE
3 CHAINE LISTE,MOIS,DATE,ENDA
5 LISTE_'JANVIER 31 JANUARY,FEVRIER 28 FEBRUARY,MARS 31 MARCH,AVRIL '
6 LISTE_LISTE!'30 APRIL,MAI 31 MAY,JUIN 30 JUNE,JUILLET 31 JULY,'
7 LISTE_LISTE!'AOUT 31 AUGUST,SEPTEMBRE 30 SEPTEMBER,OCTOBRE 31 OCTOBER,'
8 LISTE_LISTE!'NOVEMBRE 30 NOVEMBER,DECEMBRE 31 DECEMBER'
10 AFFICHER[/,'DATE EN FRANCAIS : '];LIRE DATE
11 I_1;*POINTEUR EXPLORATION CHAINE DATE
13* TEST SUR LA PRESENCE DE 'LE' EN DEBUT DE DATE
14 &TEST();SI GRL(DATE,1,I)='LE' ALORS ALLER EN 17
15 I_PTR(DATE,1,'0123456789');DATE_'LE'!' '!SCH(DATE,I,'?');I_3;&ERR(0)
17* TESTS DU JOUR,DU MOIS,DE L'ANNEE
18 &TEST();JOUR_CNB(DATE,I,I);SI JOUR<=0 ALORS &ERR(10)
19 &TEST();MOIS_GRL(DATE,I,I);PMOIS_POS(LISTE,1,MOIS!' ')
20 SI PMOIS=0 ALORS &ERR(10)
21 &TEST();ANN_CNB(DATE,I,I)
22 SI ANNEE<=1582 ALORS &ERR(10);PAS DANS LE CALENDRIER GREGORIEN
23 SI I<LGR(DATE) ALORS DEBUT DATE_SCH(DATE,1,I);&ERR(0) FIN
25* VERIFICATION DU JOUR SELON LE MOIS
26 MAXJ_CNB(LISTE,PTR(LISTE,PMOIS));*NUMERO MAX DU JOUR DANS LE MOIS
27 SI JOUR<=MAXJ ALORS ALLER EN 34
29* TEST ANNEE BISSEXTILE(SI MOIS=FEVRIER)
30 SI MOIS#'FEVRIER' OU JOUR>29 ALORS &ERR(10)
31 SI &RESTE(4)#0 OU &RESTE(100)=0 ET &RESTE(400)#0 ALORS &ERR(10)
33* IMPRESSION DE LA DATE EN ANGLAIS
34 ENDA_GRL(LISTE,PTR(LISTE,PMOIS))!' '!CCA(JOUR)!','!CCA(ANNEE)
35 AFFICHER[5X,U,/]ENDA;PAUSE;ALLER EN 10
40 PROCEDURE &TEST();*TEST SI FIN DE CHAINE
41 SI I<=LGR(DATE) ALORS RETOUR
42 AFFICHER[5X,'DATE INCOMPLETE',2/];RETOUR EN 10
50 PROCEDURE &ERR(K) LOCAL K;AFFICHER[5X,'DATE ERRONNEE',/]
51 SI K>0 ALORS RETOUR EN K
52 AFFICHER['DATE MODIFIEE : ']DATE;RETOUR
60 PROCEDURE &RESTE(DIV) LOCAL DIV
61* CALCUL DU RECTE DE LA DIVISION ENTIERE DE ANNEE PAR DIV
62 RESULTAT ANNEE-DIV*ENT(ANNEE/DIV)

