1*AUTEUR: PASCAL BOURGUIGNON
2*DATE: LUNDI 7 JUILLET 2014
3*DESCRIPTION: ALLUNISSAGE DU LEM.
4*EN COURS
5*
10* CONSTANTE DE LA GRAVITE:
11 G_6.67384E-11;* (M^3/KG/S^2) (N(M/KG)^2) ; F_G*M1*M2/R^2
12 PI_3.141592
13* PARAMETRES DE LA LUNE:
14 EQRAD_1738.14E3;* (M) RAYON EQUATORIAL DE LA LUNE
15 LMASS_7.3477E22;* (KG) MASSE DE LA LUNE
16* PARAMETRES DU LEM:
17 MLEM_10149.0;* (KG) MASSE DU LEM (INCLUS LES PROPELANTS)
18 PMASS_8200.0;* (KG) MASSE DES PROPELANTS
19 IMPUL_3050.0;* (N.s/kg) IMPULSION SPECIFIQUE DU DPS (DESCENT PROPULSION SYSTEM).
20 MPOUS_45040.0;* (N) POUSSEE MAXIMUM
21* LA POUSSEE PEUT ETRE MODULEE ENTRE 10% ET 60% DU MAXIMUM (OU 100%).
22* ORBITE D'APOLLO XI:
23 PERIS_100.9E3+EQRAD;* (M) PERSIELENE
24 APOSE_112.4E3+EQRAD;* (M) APOSELENE
25 &TITRE();CHAINE REP
26 T_0.0;X_APOSE;Y_0.0;VX_0.0;VY_1.0;R_PI/2
27 D_RAC(X*X+Y*Y);A_D-EQRAD;DT_SI A>1000.0 ALORS 60.0 SINON 10.0
28 &AFFIC(T,DT,X,Y,VX,VY,R,D,A);LIRE REP
29 T_T+DT;X_X+VX*DT;Y_Y+VY*DT;ALLER EN 27
100 TERMINER

200 PROCEDURE &AFFIC(T,DT,X,Y,VX,VY,R,D,A) LOCAL T,DT,X,Y,VX,VY,R,D,A
201 SI 0#T-ENT(T/DT/10)*DT*10 ALORS ALLER EN 203
202 AFFICHER[/,' TEMPS|      POSITION      |  VITESSE   |DIRECTION|  ALTITUDE  |']
203 AFFICHER[/,F5.0,'|',2F8.1,'|',2F4.1,'|',F5.3,'|',F8.3,'|']T,X,Y,VX,VY,R,A
204 RETOUR
205 PROCEDURE &TITRE()
206 AFFICHER[2/,30X,24'*',/,30X,'*  ALLUNISSAGE DU LEM  *',/,30X,24'*',2/]
207 AFFICHER 'VOUS ETES DANS LE LEM QUI VIENT JUSTE DE SE DETACHER DU MODULE DE COMMANDE.'
208 AFFICHER 'MALHEUREUSEMENT, L''ORDINATEUR DU LEM EST EN PANNE, VOUS DEVEZ EFFECTUER '
209 AFFICHER 'L''ALLUNISSAGE MANUELLEMENT EN PARTANT DE L''ORBITE LUNAIRE.'
210 AFFICHER 'A CHAQUE ETAPE VOUS POUVEZ ENTRER UNE COMMANDE OU UNE SAISIE VIDE.'
211 AFFICHER 'D999 ACTIVE LE DPS AU POURCENTAGE INDIQUE. LES POURCENTAGES VALIDES'
212 AFFICHER '     SONT: 0 100 ET DE 10 A 60. EXEMPLES: M55 M100 M0'
213 AFFICHER 'R999 ACTIVE LE RCS (ROTATION) AU POURCENTAGE INDIQUE (DE -100 A 100).'
214 AFFICHER 'T999 ACTIVE LE RCS (TRANSLATION) AU POURCENTAGE INDIQUE (DE -100 A 100).'
215 AFFICHER[2/]
216 RETOUR
