1*BOURGUIGNON PASCAL 16 06 2014
2*AFIC
3*AFFICHE LE CONTENU D'UN FICHIER
4*
5*ENCOURS
10 MAXNR_65535
12 CHAINE NFIC;NFIC_&DNFIC('FICHIER A LISTER')
14 FAIRE 30 POUR NR_1 JUSQUA MAXNR
16 CHARGER REC,NR,NFIC,RES
18 SI RES=-2 ALORS DEBUT &EFIC(NFIC);TERMINER FIN
20 SI RES=-1 ALORS ALLER EN 30
22 SI RES=0 ALORS DEBUT &AVAL(NR,REC);ALLER EN 30 FIN
24 SI RES=1 ALORS DEBUT &AVEC(NR,REC);LIBERER REC;ALLER EN 30 FIN
26 SI RES=2 ALORS DEBUT &AMAT(NR,REC);LIBERER REC;ALLER EN 30 FIN
28 SI RES=3 ALORS DEBUT &ACHA(NR,REC);LIBERER REC;ALLER EN 30 FIN
30*FIN
32 AFFICHER[/,'*** FIN DE FICHIER ****',/]
34 TERMINER
100 PROCEDURE &AVAL(NR,VAL) LOCAL NR,VAL
101 AFFICHER[/,F5.0,': (NOMBRE)  ',U]NR,VAL
102 RETOUR
110 PROCEDURE &AVEC(NR,VEC) LOCAL NR,VEC
111 AFFICHER[/,F5.0,': (VECTEUR) ',U]NR,VEC
112 RETOUR
120 PROCEDURE &AMAT(NR,MAT) LOCAL NR,MAT
121 AFFICHER[/,F5.0,': (MATRICE) ',U]NR,MAT
122 RETOUR
130 PROCEDURE &ACHA(NR,CHA) LOCAL NR,CHA
131 AFFICHER[/,F5.0,': (CHAINE)  ',U]NR,CHA
132 RETOUR
140 PROCEDURE &EFIC(NFIC) LOCAL NFIC
141 AFFICHER[/,'ERREUR: FICHIER ''',U,''' INEXISTANT.',/]NFIC
142 TERMINER
200 PROCEDURE &DNFIC(INVIT) LOCAL INVIT;CHAINE FIC
201 AFFICHER[/,U,': ']INVIT;LIRE FIC;RESULTAT FIC
