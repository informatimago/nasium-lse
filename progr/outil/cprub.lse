1*BOURGUIGNON PASCAL 29 07 2014
2*CPRUB
3*COPIE UN RUBAN CONTENANT DES ENREGISTREMENTS TERMINES PAR X-OFF 
4*JUSQU'A UN ENREGISTREMENT CONTENANT '***'.
5*TOURNE

10 CHAINE REC
15 &RUBIN()
20 REC_&LIRUB()
25 AFFICHER['LU DU RUBAN: ',U,/]REC
30 &PERFO(REC!XOFF)
35 SI REC#'***' ALORS ALLER EN 20
40 TERMINER

50 PROCEDURE &EXEMP() LOCAL REC,I;CHAINE REC
55 &RUBIN()
60 FAIRE 65 POUR I_1 JUSQUA 10 
65 &PERFO('ENREGISTREMENT NUMERO '!CCA(I)!' CONTENANT CECI.'!XOFF)
66 &PERFO('***'!XOFF)
70 RETOUR

100 PROCEDURE &RUBIN()
101 CHAINE LIRON,PERON,XOFF,PEROF,LIROF
102 LIRON_EQC(17);PERON_EQC(18);XOFF_EQC(19);PEROF_EQC(20);LIROF_EQC(21)
103 RETOUR
104 PROCEDURE &PERFO(DONNE) LOCAL DONNE
105 AFFICHER[U,U,U]PERON,DONNE,PEROF
106 RETOUR
107 PROCEDURE &LIRUB() LOCAL DONNE;CHAINE DONNE
108 AFFICHER[U]LIRON;LIRE DONNE;AFFICHER[U]LIROF
109 RESULTAT DONNE
