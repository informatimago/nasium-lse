1*BOURGUIGNON PASCAL 29 07 2014
2*PERFO
3*PERFORE DES DONNEES SUR UN RUBAN
4*
5*ENCOURS

10 CHAINE NFIC
15 &RUBIN()
20 AFFICHER['NOM DU FICHIER A PERFORER: '];LIRE NFIC
25 AFFICHER['PREMIER ET DERNIER NUMERO D''ENREGISTREMENT: '];LIRE DEB,END
30 &PFIC(NFIC,DEB,END)
35 AFFICHER 'N''OUBLIEZ PAS D''ARCHIVER LE RUBAN (COMMANDE AR).'
40 TERMINER

100*********************************************************************
101*PROCEDURES POUR ACTIVER ET DESACTIVER LE LECTEUR ET LE PERFORATEUR 
102*DE RUBANS. APPELER &RUBIN() POUR INITIALIZER LES CONSTANTES.
103*********************************************************************
104 PROCEDURE &RUBIN()
105 CHAINE LIRON,PERON,XOFF,PEROF,LIROF
106 LIRON_EQC(17);PERON_EQC(18);XOFF_EQC(19);PEROF_EQC(20);LIROF_EQC(21)
107 RETOUR
108*********************************************************************
109 PROCEDURE &PERFO(DONNE) LOCAL DONNE
110 AFFICHER[U,U,U]PERON,DONNE,PEROF
111 RETOUR
112*********************************************************************
113 PROCEDURE &LIRUB() LOCAL DONNE;CHAINE DONNE
114 AFFICHER[U]LIRON;LIRE DONNE;AFFICHER[U]LIROF
115 RESULTAT DONNE
116*********************************************************************
117 PROCEDURE &LIRON();AFFICHER[U]LIRON;RETOUR
118 PROCEDURE &PERON();AFFICHER[U]PERON;RETOUR
119 PROCEDURE &XOFF(); AFFICHER[U]XOFF; RETOUR
120 PROCEDURE &PEROF();AFFICHER[U]PEROF;RETOUR
121 PROCEDURE &LIROF();AFFICHER[U]LIROF;RETOUR
122*********************************************************************
123*PROCEDURES POUR PERFORER UN FICHIER SELON LE FORMAT SUIVANT:
124*********************************************************************
125* ficher ::= entete { enregistrement } fin .
126* entete ::= 'AP' XOFF 'CHARG' XOFF 'EX' XOFF '1' XOFF nomfic XOFF .
127* nomfic ::= /[A-Z][A-Z0-9]{0,4}/ .
128* fin    ::= '0 99' XOFF .
129* enregistrement ::= enrnombre | enrvecteur | enrmatrice | enrchaine .
130* enrnombre  ::= enrno ' 0' XOFF nombre XOFF .
131* enrvecteur ::= enrno ' 1' XOFF vecteur XOFF fintableau XOFF .
132* enrmatrice ::= enrno ' 2' XOFF matrice XOFF fintableau XOFF .
133* fintableau ::= '*' .
134* enrchaine  ::= enrno ' 3' XOFF numchunk XOFF {chunk} .
135* numchunk   ::= nombre .
136* chunk      ::= textchunk | codechunk .
137* textchunk  ::= '1' XOFF text XOFF .
138* codechunk  ::= '2' XOFF longueur {' ' code} XOFF .
139* longueur   ::= nombre .
140* code       ::= nombre .
141* text       ::= /any printable character/ .
142*********************************************************************
143 PROCEDURE &PFIC(NFIC,DEB,END) LOCAL NFIC,DEB,END,ENR,STAT
144 &PENT(NFIC)
145 FAIRE 153 POUR ENRNO_DEB JUSQUA END
146 CHARGER ENR,ENRNO,NFIC,STAT
147 SI STAT=-2 ALORS DEBUT &ERROR('PAS DE FICHIER NOMME '!NFIC);RETOUR FIN
148 AFFICHER 'ENREGISTREMENT ',ERRNO,STAT
149 SI STAT=0 ALORS DEBUT &PNOMB(ENRNO,ENR);ALLER EN 153 FIN
150 SI STAT=1 ALORS DEBUT &PVECT(ENRNO,ENR);ALLER EN 153 FIN
151 SI STAT=2 ALORS DEBUT &PMATR(ENRNO,ENR);ALLER EN 153 FIN
152 SI STAT=3 ALORS DEBUT &PCHAI(ENRNO,ENR);ALLER EN 153 FIN
153*FIN FICHIER
154 &PFIN()
155 RETOUR
156*********************************************************************
157 PROCEDURE &PENT(NFIC)LOCAL NFIC
158 &PERON()
159 AFFICHER['AP'];&XOFF();AFFICHER['CHARG'];&XOFF()
160 AFFICHER['EX'];&XOFF();AFFICHER['1'];&XOFF()
161 AFFICHER[U]NFIC;&XOFF()
162 &PEROF()
163 RETOUR
164*********************************************************************
165 PROCEDURE &PNOMB(ENRNO,NOMB) LOCAL ENRNO,NOMB
166 &PERON();AFFICHER[U,U]ENRNO,0;&XOFF();AFFICHER[U]NOMB;&XOFF();&PEROF()
167 RETOUR
168*********************************************************************
169 PROCEDURE &PVECT(ENRNO,VECT) LOCAL ENRNO,VECT
170 &PERON();AFFICHER[U,U]ENRNO,1;&XOFF();AFFICHER[U]VECT;&XOFF();&FINTA();&PEROF()
171 RETOUR
172*********************************************************************
173 PROCEDURE &PMATR(ENRNO,MATR) LOCAL ENRNO,MATR
174 &PERON();AFFICHER[U,U]ENRNO,2;&XOFF();AFFICHER[U]MATR;&XOFF();&FINTA();&PEROF()
175 RETOUR
176*********************************************************************
177 PROCEDURE &FINTA();AFFICHER['*'];&XOFF();RETOUR
178*********************************************************************
179 PROCEDURE &PFIN();&PERON();AFFICHER[U,U]0,99;&XOFF();&PEROF();RETOUR
180*********************************************************************
181 PROCEDURE &PCHAI(ENRNO,CHAI) LOCAL ENRNO,CHAI,NSEG,I,SEGTY,DEB,LON
182 NSEG_&CNSEG(CHAI)
183 &PERON();AFFICHER[U,U]ENRNO,3;&XOFF();AFFICHER[U]NSEG;&XOFF()
184 FAIRE 186 POUR I_1 JUSQUA NSEG
185 &SEGM(CHAI,I,SEGTY,DEB,LON)
186 SI SEGTY=1 ALORS &PSTXT(CHAI,DEB,LON) SINON &PSCOD(CHAI,DEB,LON)
187 &PEROF();RETOUR
188*********************************************************************
189 PROCEDURE &PSTXT(CHAI,DEB,END) LOCAL CHAI,DEB,LON
190 AFFICHER[U]1;&XOFF();AFFICHER[U]SCH(CHAI,DEB,LON);&XOFF()
191*********************************************************************
192 PROCEDURE &PSCOD(CHAI,DEB,LON) LOCAL CHAI,DEB,LON,I
193 AFFICHER[U]2;&XOFF();AFFICHER[U]LON
194 FAIRE 195 POUR I_1 JUSQUA LON
195 AFFICHER[U]EQN(CHAI,DEB+I-1)
196 &XOFF();RETOUR
197*********************************************************************
198 PROCEDURE &SEGM(CHAI,SEGNO,SEGTY,DEB,LON) LOCAL CHAI,SEGNO,P,L,D,N,C,T,I,J
199 L_LGR(CHAI);N_1;T_0
200 ALLER EN 208
201*RECHERCHE
202 C_EQN(CHAI,D);SEGTY_SI C<32 ALORS 2 SINON 1
203 N_D;T_SEGTY;ALLER EN 206
204*SEGMENT
205 N_N+1;C_EQN(CHAI,N);T_SI C<32 ALORS 2 SINON 1
206 SI T=SEGTY ET N<L ALORS ALLER EN 204
207 SEGNO_SEGNO-1
208 SI SEGNO>0 ET N<L ALORS DEBUT SEGTY_T;D_N;ALLER EN 201 FIN
209 SI N=D ALORS DEBUT SEGTY_0;DEB_0;LON_0 FIN SINON DEBUT DEB_D;LON_N-D FIN
210 RETOUR
214*********************************************************************
215 PROCEDURE &CNSEG(CHAI) LOCAL CHAI,SEGNO,SEGTY,DEB,LON,L
216 SEGNO_1;&SEGM(CHAI,SEGNO,SEGTY,DEB,LON);L_LGR(CHAI)
217*DEBUT COMPTE
218 SI DEB+LON=L ALORS ALLER EN 221
219 SEGNO_SEGNO+1;&SEGM(CHAI,SEGNO,SEGTY,DEB,LON)
220 ALLER EN 217
221*FIN COMPTE
222 RESULTAT SEGNO
223*********************************************************************
224 PROCEDURE &PTEST()
225 &PNOMB(1,3.141592)
226 TABLEAU V[3];V[1]_1.1;V[2]_2.2;V[3]_3.3
227 &PVECT(2,V)
228 TABLEAU M[3,3]
229 M[1,1]_1.1;M[1,2]_1.2;M[1,3]_1.3
230 M[2,1]_2.1;M[2,2]_2.2;M[2,3]_2.3
231 M[3,1]_3.1;M[3,2]_3.2;M[3,3]_3.3
232 &PMATR(3,M)
233 CHAINE UN,DEUX
234 UN_'SIMPLE CHAINE TENANT SUR UNE SEULE LIGNE AVEC SEULEMENT DES CARACTERES IMPRIMABLES.'
235 DEUX_'CHAINE A DEUX LIGNES'!EQC(13)!EQC(10)!'ET AVEC UN CODE BEL '!EQC(7)!' ICI.'
236 &PCHAI(4,UN)
237 &PCHAI(5,DEUX)
238 &PFIN()
239 RETOUR
240*********************************************************************
241 PROCEDURE &TSEG() LOCAL CH,N,T,D,L;CHAINE CH
242 CH_'PREMIERE LIGNE'!EQC(13)!EQC(10)!'DEUXIEME LIGNE '!EQC(7)!' ET CLOCHE!'
243 N_0;AFFICHER[/]
244*SEGMENT
245 N_N+1;&SEGM(CH,N,T,D,L)
246 SI T=0 ALORS ALLER EN 253
247 AFFICHER[U,' ',U,' ']SI T=1 ALORS 'TEXT' SINON 'CODE',L
248 SI T=1 ALORS DEBUT AFFICHER[U,/]SCH(CH,D,L);ALLER EN 244 FIN
249 FAIRE 250 POUR I_D JUSQUA D+L-1
250 AFFICHER[U]EQN(CH,I)
251 AFFICHER[/]
252 ALLER EN 244
253*FIN SEGMENT
254 RETOUR
