1*BOURGUIGNON PASCAL 29 07 2014
2*PERFO
3*PERFORE DES DONNEES SUR UN RUBAN
4*
5*TOURNE

10 CHAINE NFIC
12 &RUBIN()
14 AFFICHER[/,'NOM DU FICHIER A PERFORER: '];LIRE NFIC
16 AFFICHER[/,'PREMIER ET DERNIER NUMERO D''ENREGISTREMENT: '];LIRE DEB,END
18 AFFICHER[/]
20 &PFIC(NFIC,DEB,END)
22 AFFICHER[/,'N''OUBLIEZ PAS D''ARCHIVER LE RUBAN (COMMANDE AR).',/]
24 TERMINER

100*********************************************************************
101*PROCEDURES POUR ACTIVER ET DESACTIVER LE LECTEUR ET LE PERFORATEUR 
102*DE RUBANS. APPELER &RUBIN() POUR INITIALIZER LES CONSTANTES.
103*********************************************************************
104 PROCEDURE &RUBIN()
105 CHAINE LIRON,PERON,XOFF,PEROF,LIROF
106 LIRON_EQC(17);PERON_EQC(18);XOFF_EQC(19);PEROF_EQC(20);LIROF_EQC(21)
107 RETOUR
108*********************************************************************
109 PROCEDURE &PERFO(DONNE) LOCAL DONNE
110 AFFICHER[U,U,U]PERON,DONNE,PEROF
111 RETOUR
112*********************************************************************
113 PROCEDURE &LIRUB() LOCAL DONNE;CHAINE DONNE
114 AFFICHER[U]LIRON;LIRE DONNE;AFFICHER[U]LIROF
115 RESULTAT DONNE
116*********************************************************************
117 PROCEDURE &LIRON();AFFICHER[U]LIRON;RETOUR
118 PROCEDURE &PERON();AFFICHER[U]PERON;RETOUR
119 PROCEDURE &XOFF(); AFFICHER[U]XOFF; RETOUR
120 PROCEDURE &PEROF();AFFICHER[U]PEROF;RETOUR
121 PROCEDURE &LIROF();AFFICHER[U]LIROF;RETOUR
122*********************************************************************
123*PROCEDURES POUR PERFORER UN FICHIER SELON LE FORMAT SUIVANT:
124*********************************************************************
125* ficher ::= entete { enregistrement } fin .
126* entete ::= 'AP' XOFF 'CHARG' XOFF 'EX' XOFF '1' XOFF nomfic XOFF .
127* nomfic ::= /[A-Z][A-Z0-9]{0,4}/ .
128* fin    ::= '0 99' XOFF .
129* enregistrement ::= enrnombre | enrvecteur | enrmatrice | enrchaine .
130* enrnombre  ::= enrno ' 0' XOFF nombre XOFF .
131* enrvecteur ::= enrno ' 1' XOFF vecteur XOFF fintableau XOFF .
132* enrmatrice ::= enrno ' 2' XOFF matrice XOFF fintableau XOFF .
133* fintableau ::= '*' .
134* enrchaine  ::= enrno ' 3' XOFF numchunk XOFF {chunk} .
135* numchunk   ::= nombre .
136* chunk      ::= textchunk | codechunk .
137* textchunk  ::= '1' XOFF text XOFF .
138* codechunk  ::= '2' XOFF longueur {' ' code} XOFF .
139* longueur   ::= nombre .
140* code       ::= nombre .
141* text       ::= /any printable character/ .
142*********************************************************************
143 PROCEDURE &PFIC(NFIC,DEB,END) LOCAL NFIC,DEB,END,ENR,STAT
144 &PENT(NFIC)
145 FAIRE 153 POUR ENRNO_DEB JUSQUA END
146 CHARGER ENR,ENRNO,NFIC,STAT
147 SI STAT=-2 ALORS DEBUT &ERROR('PAS DE FICHIER NOMME '!NFIC);RETOUR FIN
148 AFFICHER 'ENREGISTREMENT ',ENRNO,STAT
149 SI STAT=0 ALORS DEBUT &PNOMB(ENRNO,ENR);ALLER EN 153 FIN
150 SI STAT=1 ALORS DEBUT &PVECT(ENRNO,ENR);ALLER EN 153 FIN
151 SI STAT=2 ALORS DEBUT &PMATR(ENRNO,ENR);ALLER EN 153 FIN
152 SI STAT=3 ALORS DEBUT &PCHAI(ENRNO,ENR);ALLER EN 153 FIN
153*FIN FICHIER
154 &PFIN()
155 RETOUR
156*********************************************************************
157 PROCEDURE &PENT(NFIC)LOCAL NFIC
158 &PERON()
159 AFFICHER['AP'];&XOFF();AFFICHER['CHARG'];&XOFF()
160 AFFICHER['EX'];&XOFF();AFFICHER['1'];&XOFF()
161 AFFICHER[U]NFIC;&XOFF()
162 &PEROF()
163 RETOUR
164*********************************************************************
165 PROCEDURE &PNOMB(ENRNO,NOMB) LOCAL ENRNO,NOMB
166 &PERON();AFFICHER[U,' ',U]CCA(ENRNO),'0';&XOFF();AFFICHER[U]CCA(NOMB);&XOFF();&PEROF()
167 RETOUR
168*********************************************************************
169 PROCEDURE &PVECT(ENRNO,VECT) LOCAL ENRNO,VECT
170 &PERON();AFFICHER[U,' ',U]CCA(ENRNO),'1';&XOFF();AFFICHER[U]VECT;&XOFF();&FINTA();&PEROF()
171 RETOUR
172*********************************************************************
173 PROCEDURE &PMATR(ENRNO,MATR) LOCAL ENRNO,MATR
174 &PERON();AFFICHER[U,' ',U]CCA(ENRNO),'2';&XOFF();AFFICHER[U]MATR;&XOFF();&FINTA();&PEROF()
175 RETOUR
176*********************************************************************
177 PROCEDURE &FINTA();AFFICHER['*'];&XOFF();RETOUR
178*********************************************************************
179 PROCEDURE &PFIN();&PERON();AFFICHER['0 99'];&XOFF();&PEROF();RETOUR
180*********************************************************************
181 PROCEDURE &PCHAI(ENRNO,CHAI) LOCAL ENRNO,CHAI,NSEG,I,SEGTY,DEB,LON
182 NSEG_&CNSEG(CHAI)
183 &PERON();AFFICHER[U,' ',U]CCA(ENRNO),'3';&XOFF();AFFICHER[U]CCA(NSEG);&XOFF()
184 FAIRE 186 POUR I_1 JUSQUA NSEG
185 &SEGM(CHAI,I,SEGTY,DEB,LON)
186 SI SEGTY=1 ALORS &PSTXT(CHAI,DEB,LON) SINON &PSCOD(CHAI,DEB,LON)
187 &PEROF();RETOUR
188*********************************************************************
189 PROCEDURE &PSTXT(CHAI,DEB,LON) LOCAL CHAI,DEB,LON
190 AFFICHER[U]'1';&XOFF();AFFICHER[U]SCH(CHAI,DEB,LON);&XOFF()
191 RETOUR
192*********************************************************************
193 PROCEDURE &PSCOD(CHAI,DEB,LON) LOCAL CHAI,DEB,LON,I
194 AFFICHER[U]'2';&XOFF();AFFICHER[U]CCA(LON)
195 FAIRE 196 POUR I_1 JUSQUA LON
196 AFFICHER[' ',U]CCA(EQN(CHAI,DEB+I-1))
197 &XOFF();RETOUR
198*********************************************************************
199 PROCEDURE &SEGM(CHAI,SEGNO,SEGTY,DEB,LON) LOCAL CHAI,SEGNO,P,L,D,N,C,T,I,J
200 L_LGR(CHAI);SEGTY_0;T_0;D_1;N_1
201 ALLER EN 209
202*RECHERCHE
203 C_EQN(CHAI,D);SEGTY_SI C<32 ALORS 2 SINON 1
204 N_D;T_SEGTY
205*SEGMENT
206 N_N+1;C_EQN(CHAI,N);T_SI C<32 ALORS 2 SINON 1
207 SI T=SEGTY ET N<L ALORS ALLER EN 205
208 SEGNO_SEGNO-1
209 SI SEGNO=0 ALORS DEBUT DEB_D;LON_N-D+SI T=SEGTY ALORS 1 SINON 0;RETOUR FIN
210 SI N>=L ALORS DEBUT SEGTY_0;DEB_0;LON_0;RETOUR FIN
211 D_N;ALLER EN 202
212*********************************************************************
213 PROCEDURE &CNSEG(CHAI) LOCAL CHAI,SEGNO,SEGTY,DEB,LON
214 SEGNO_1
215 &SEGM(CHAI,SEGNO,SEGTY,DEB,LON)
216 SI SEGTY=0 ALORS RESULTAT SEGNO-1 SINON DEBUT SEGNO_SEGNO+1;ALLER EN 215 FIN
217*********************************************************************
218 PROCEDURE &ERROR(MSG) LOCAL MSG;AFFICHER[/,'*** ERREUR: ',U,/]MSG;RETOUR
219*********************************************************************
220 PROCEDURE &PTEST();&RUBIN()
221 &PNOMB(1,3.141592)
222 TABLEAU V[3];V[1]_1.1;V[2]_2.2;V[3]_3.3
223 &PVECT(2,V)
224 TABLEAU M[3,3]
225 M[1,1]_1.1;M[1,2]_1.2;M[1,3]_1.3
226 M[2,1]_2.1;M[2,2]_2.2;M[2,3]_2.3
227 M[3,1]_3.1;M[3,2]_3.2;M[3,3]_3.3
228 &PMATR(3,M)
229 CHAINE UN,DEUX
230 UN_'SIMPLE CHAINE TENANT SUR UNE SEULE LIGNE AVEC SEULEMENT DES CARACTERES IMPRIMABLES.'
231 DEUX_'CHAINE A DEUX LIGNES'!EQC(13)!EQC(10)!'ET AVEC UN CODE BEL '!EQC(7)!' ICI.'
232 &PCHAI(4,UN)
233 &PCHAI(5,DEUX)
234 &PFIN()
235 RETOUR
236*********************************************************************
237 PROCEDURE &TSEG() LOCAL CH,N,T,D,L;CHAINE CH
238 CH_'PREMIERE LIGNE'!EQC(13)!EQC(10)!'DEUXIEME LIGNE '!EQC(7)!' ET CLOCHE!'
239 N_0;AFFICHER[/]
240*SEGMENT
241 N_N+1;&SEGM(CH,N,T,D,L)
242 SI T=0 ALORS ALLER EN 249
243 AFFICHER[U,' ',U,' ']SI T=1 ALORS 'TEXT' SINON 'CODE',L
244 SI T=1 ALORS DEBUT AFFICHER[U,/]SCH(CH,D,L);ALLER EN 240 FIN
245 FAIRE 246 POUR I_D JUSQUA D+L-1
246 AFFICHER[U]EQN(CH,I)
247 AFFICHER[/]
248 ALLER EN 240
249*FIN SEGMENT
250 RETOUR
