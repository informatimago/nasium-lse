1*Auteur: Pascal Bourguignon
2*Description: programme de test boucle FAIRE JUSQUA

10 AFFICHER[/,'0) QUITTER.']
11 AFFICHER[/,'1) BOUCLES QUI DOIVENT FONCTIONNER.']
12 AFFICHER[/,'2) BOUCLES IMBRIQUEES QUI DOIVENT DONNER UNE ERREUR.']
13 AFFICHER[/,'VOTRE CHOIX:'];LIRE CHOIX
14 SI CHOIX=0 ALORS TERMINER
15 SI CHOIX=1 ALORS DEBUT &GOOD(); ALLER EN 10 FIN
16 SI CHOIX=2 ALORS DEBUT &FAIL1(); ALLER EN 10 FIN
17 SI CHOIX=3 ALORS DEBUT &FAIL2(); ALLER EN 10 FIN
18 ALLER EN 10


100 PROCEDURE &GOOD()
110 I_0
120 AFFICHER[2/,'FAIRE POUR I_1 PAS 2 JUSQUA 15',/]
130 FAIRE 140 POUR I_1 PAS 2 JUSQUA 15
140 AFFICHER I
150 AFFICHER[2/,'FAIRE POUR I_1 PAS 2 TANT QUE 3*i<25',/]
160 FAIRE 170 POUR I_1 PAS 2 TANT QUE 3*I<25
170 AFFICHER I
180 AFFICHER[2/,'FAIRE POUR I_1 JUSQUA 3']
190 FAIRE 240 POUR I_1 JUSQUA 3
200 AFFICHER[/,3X,'FAIRE POUR J_1 JUSQUA 3']
210 FAIRE 240 POUR J_1 JUSQUA 3
220 AFFICHER[/,6X,'FAIRE POUR K_1 JUSQUA 3']
230 FAIRE 240 POUR K_1 JUSQUA 3
240 AFFICHER[/,9X,'I=',U,'J=',U,'K=',U]I,J,K
250*
260 AFFICHER[2/,'FAIRE POUR I_1 JUSQUA 10 / NON LOCAL EXIT AT 5',/]
270 FAIRE 280 POUR I_1 JUSQUA 10
280 AFFICHER[U]I;SI 5<I ALORS ALLER EN 290
290*SUITE
300 AFFICHER[2/,'FAIRE POUR I_1 JUSQUA 0']
310 FAIRE 320 POUR I_1 JUSQUA 0 
320 AFFICHER '*** BAD IN-LOOP:',I
330 AFFICHER 'FIN:',I
340*TANT QUE FALSE
350 AFFICHER[2/,'FAIRE POUR I_1 TANT QUE 1=0']
360 FAIRE 370 POUR I_1 TANT QUE 1=0
370 AFFICHER '*** BAD IN-LOOP:',I
380 AFFICHER 'FIN:',I
390*EMBEDDED SINGLE-LINE LOOPS
400 AFFICHER[2/,'FAIRE POUR I_1 JUSQUA 2;FAIRE POUR J_1 JUSQUA 0  ON THE SAME LINE']
410 FAIRE 410 POUR I_1 JUSQUA 2;AFFICHER 'OUT: I=',I;FAIRE 410 POUR J_1 JUSQUA -1;AFFICHER '*** BAD: I=',I,'J=',J
420 AFFICHER 'FIN:',I,J
430 AFFICHER[2/,'FAIRE POUR I_1 TANT QUE I<=2;FAIRE POUR J_1 TANT QUE 1=0   ON THE SAME LINE']
440 FAIRE 440 POUR I_1 TANT QUE I<=2;AFFICHER 'OUT: I=',I;FAIRE 440 POUR J_1 TANT QUE 1=0;AFFICHER '*** BAD: I=',I,'J=',J
450 AFFICHER 'FIN:',I,J
460 RETOUR

1000* FOLLOWING TESTS SHOULD SIGNAL AN ERROR:
1010 PROCEDURE &FAIL1();* EMBEDDED LOOPS, LINO-1 FAIRE LINO ; LINO FAIRE LINO+1
1020 AFFICHER[/,'FAIRE POUR I_1 JUSQUA 10 / JUMP INTO THE EMBEDDED LOOP AT 5',/]
1030 FAIRE 1060 POUR I_1 JUSQUA 10
1040 AFFICHER[U]I;SI 5<I ALORS ALLER EN 1070
1050 AFFICHER[/,4X,'FAIRE POUR J_1 JUSQUA 10 / NON LOCAL EXIT (TWO LOOPS) AT 5',/,4X]
1060 FAIRE 1070 POUR J_1 JUSQUA 10 ;*BOUCLES ENCHEVETREES EXPRES!
1070 AFFICHER[U]J;SI 5<J ALORS ALLER EN 1080
1080 RETOUR
