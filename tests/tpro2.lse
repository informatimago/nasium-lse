1*AUTEUR: PASCAL BOURGUIGNON
2*DESCRIPTION: PROGRAMME DE TEST: PROCEDURES SOUS-PROGRAMME
10 CHAINE CH;TABLEAU T[3]
12 AFFICHER[/,'LES ARGUMENTS QUI DOIVENT ETRE MODIFIES SONT MARQUES D''UNE ETOILE.',/]
14 &PRO0();AFFICHER['APRES &PRO0():  ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
16 &PRO1(X,T[1],CH)
18 AFFICHER['APRES &PRO1():  ','*X=',U,'*Y=',U,'*Z=',U,/]X,T[1],CH
20 &PRO0();AFFICHER['APRES &PRO0():  ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
22 &PRO1(X,T[1],CH);AFFICHER['APRES &PRO1():  ','*X=',U,'*Y=',U,'*Z=',U,/]X,T[1],CH
24 &PRO0();AFFICHER['APRES &PRO0():  ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
26 &PRO2(X,T[1],CH);AFFICHER['APRES &PRO2():  ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
28 &PRO0();AFFICHER['APRES &PRO0():  ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
30 &PRO3(X,T[1],CH);AFFICHER['APRES &PRO3():  ',' X=',U,' Y=',U,'*Z=',U,/]X,T[1],CH
32 &PRO0();AFFICHER['APRES &PRO0():  ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
34 &PRO4(X,T[1],CH);AFFICHER['APRES &PRO4():  ','*X=',U,'*Y=',U,' Z=',U,/]X,T[1],CH
36 &PRO0();AFFICHER['APRES &PRO0():  ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
38 &PRO11(X,T[1],CH);AFFICHER['APRES &PRO11(): ','*X=',U,'*Y=',U,'*Z=',U,/]X,T[1],CH
40 &PRO0();AFFICHER['APRES &PRO0():  ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
42 &PRO12(X,T[1],CH);AFFICHER['APRES &PRO12(): ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
44 &PRO0();AFFICHER['APRES &PRO0():  ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
46 &PRO13(X,T[1],CH);AFFICHER['APRES &PRO13(): ',' X=',U,' Y=',U,'*Z=',U,/]X,T[1],CH
48 &PRO0();AFFICHER['APRES &PRO0():  ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
50 &PRO14(X,T[1],CH);AFFICHER['APRES &PRO14(): ','*X=',U,'*Y=',U,' Z=',U,/]X,T[1],CH
52 &PRO0();AFFICHER['APRES &PRO0():  ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
54 &PRO23(X,T[1],CH);AFFICHER['APRES &PRO23(): ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
56 &PRO0();AFFICHER['APRES &PRO0():  ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
58 &PRO24(X,T[1],CH);AFFICHER['APRES &PRO24(): ',' X=',U,' Y=',U,' Z=',U,/]X,T[1],CH
59 AFFICHER[/]
60 &FACT(9,R);AFFICHER['APRES &FACT(9,R): ',' 362880=R=',U]R
90 TERMINER
100 PROCEDURE &PRO0()
102* COMME CES VARIABLES NE SONT PAS DES PARAMETRES, ELLES SONT GLOBALES.
104 X_0;CH_'INIT';T[1]_0;T[2]_0;T[3]_0;AFFICHER[/]
106 RETOUR
108 PROCEDURE &PRO1(A,B,C)
110* COMME A,B,C NE SONT PAS LOCAL, LES ARGUMENTS SONT MODIFIES.
112 A_1;B_2;C_'HELLO'
114 RETOUR
116 PROCEDURE &PRO2(A,B,C) LOCAL A,B,C
118* COMME A,B,C SONT LOCAL, LES ARGUMENTS NE SONT PAS MODIFIES.
120 A_1;B_2;C_'HELLO'
122 RETOUR
124 PROCEDURE &PRO3(A,B,C) LOCAL A,B
126* COMME A,B SONT LOCAL, LES ARGUMENTS NE SONT PAS MODIFIES.
128* COMME C N'EST PAS LOCAL, L'ARGUMENT EST MODIFIE.
130 A_1;B_2;C_'HELLO'
132 RETOUR
134 PROCEDURE &PRO4(A,B,C) LOCAL C
136* COMME A,B NE SONT PAS LOCAL, LES ARGUMENTS SONT MODIFIES.
138* COMME C EST LOCAL, L'ARGUMENT N'EST PAS MODIFIE.
140 A_1;B_2;C_'HELLO'
142 RETOUR
144 PROCEDURE &PRO11(A,B,C)
146* LES ARGUMENTS DE &PRO11 SONT MODIFIES PAR &PRO1.
148 &PRO1(A,B,C)
150 RETOUR
152 PROCEDURE &PRO12(A,B,C)
154* LES ARGUMENTS DE &PRO12 POURRAIENT ETRE MODIFIES, MAIS NE LE SONT PAS PAR &PRO2.
156 &PRO2(A,B,C)
158 RETOUR
160 PROCEDURE &PRO13(A,B,C)
162 &PRO3(A,B,C)
164 RETOUR
166 PROCEDURE &PRO14(A,B,C)
168 &PRO4(A,B,C)
170 RETOUR
172 PROCEDURE &PRO23(A,B,C) LOCAL C
174 &PRO3(A,B,C)
176 RETOUR
178 PROCEDURE &PRO24(A,B,C) LOCAL A,B
180 &PRO4(A,B,C)
182 RETOUR
184 PROCEDURE &FACT(N,R) LOCAL N
185 SI N=1 ALORS R_1 SINON DEBUT N_N-1;&FACT(N,R);R_R*(N+1) FIN
186 RETOUR
